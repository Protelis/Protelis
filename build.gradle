apply plugin: 'java'
apply from: 'eclipse.gradle'
apply from: 'codequality.gradle'
apply plugin: 'project-report'
apply from: 'maven.gradle'
apply plugin: 'build-dashboard'
apply from: 'signing.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}

ext {
    def vfile = new File("${projectDir}/version.info")
    try {
        git = org.ajoberstar.grgit.Grgit.open("${projectDir}")
        branch = git.branch.current.name
        if (!branch.equals("master") || !branch.contains("release")) {
            project.version = "${project.version}-${branch}-${git.head().abbreviatedId}"
        }
        vfile.text = project.version
    } catch (Exception ex) {
        println("No Git repository info available, falling back to file")
        project.version = vfile.text
    }
}

apply plugin: 'me.tatarka.retrolambda'
retrolambda {
  javaVersion JavaVersion.VERSION_1_7
  defaultMethods true
  incremental true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = gradleVersionToUse
}

// General configuration

compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes 'Implementation-Title': artifactId, 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

configurations {
    doc {
        transitive false
    }
    compile {
        exclude module: 'org.eclipse.xtext.dependencies'
        exclude group: 'asm', module: 'asm'
    }
    doclet
}

dependencies {


    compile "commons-codec:commons-codec:$commonsCodecVersion"
    compile "commons-io:commons-io:$commonsIOVersion"
    compile "org.apache.commons:commons-math3:$math3Version"
    compile "net.sf.trove4j:trove4j:$trove4jVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile "org.apache.commons:commons-lang3:$lang3Version"
    compile "org.danilopianini:javalib-java7:$javalib7Version"
    compile "org.danilopianini:javalib:$javalibVersion"
    compile "org.springframework:spring-core:$springVersion"
    compile "org.protelis:protelis.parser:$parserVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.eclipse.emf:org.eclipse.emf.mwe.core:$mweVersion"
    compile "org.eclipse.emf:org.eclipse.emf.mwe.utils:$mweVersion"
    compile "com.google.code.findbugs:findbugs:$findBugsVersion"
    compile "net.sourceforge.streamsupport:streamsupport-flow:$streamVersion"

    testCompile "junit:junit:$junitVersion"

    testRuntime "ch.qos.logback:logback-classic:1.1.3"
    
    doclet "org.jboss.apiviz:apiviz:$apivizVersion"
    
    pmd(
        "net.sourceforge.pmd:pmd-core:$pmdVersion",
        "net.sourceforge.pmd:pmd-vm:$pmdVersion",
        "net.sourceforge.pmd:pmd-plsql:$pmdVersion",
        "net.sourceforge.pmd:pmd-jsp:$pmdVersion",
        "net.sourceforge.pmd:pmd-xml:$pmdVersion",
        "net.sourceforge.pmd:pmd-java:$pmdVersion"
    )
}

// Javadoc Configuration

task aggregateJavadoc(type: Javadoc) {
    source configurations.doc.collect { zipTree(it) }
    source sourceSets.main.allJava
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath
    include '**/*.java'
    destinationDir file("$project.buildDir/docs/javadoc/")
    failOnError = false
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
    options.showAll()
    options.addBooleanOption('nopackagediagram', true)
    options.addStringOption('Xdoclint:none', '-quiet')
    configure(options) {
        windowTitle "$projectLongName version $project.version Javadoc API"
        docTitle "$projectLongName $project.version reference API"
        links 'http://docs.oracle.com/javase/8/docs/api/'
        links 'http://trove4j.sourceforge.net/javadocs/'
        doclet 'org.jboss.apiviz.APIviz'
        docletpath file(configurations.doclet.asPath)
    }
}

// Artifacts configuration

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: aggregateJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

defaultTasks 'wrapper', 'clean', 'build', 'check', 'assemble', 'install', 'aggregateJavadoc', 'buildDashboard'

task fatJar(type: Jar) {
    baseName = project.name + '-redist'
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}
